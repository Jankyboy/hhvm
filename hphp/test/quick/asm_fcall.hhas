#
# Test some function call junk
#

.function N caller(N $who, N $arg) {
  .declvars $ret;
  NullUninit
  NullUninit
  CGetL $arg
  NullUninit
  NullUninit
  CGetL $who
  FCallFuncD <> 1 1 "" "" - "" "HH\\dynamic_fun"
  FCallFunc <> 1 1 "" "" - ""

  SetL $ret
  PopC
  NullUninit
  NullUninit
  NullUninit
  NullUninit
  CGetL $ret
  FCallFuncD <> 1 1 "" "" - "" "HH\\dynamic_fun"
  FCallFunc <> 0 1 "" "" - ""
  PopC

  True
  RetC
}

.function [dyn_callable] N b() {
  String "In b() now\n"
  Print
  PopC

  True
  RetC
}

.function [dyn_callable] N a(N $arg) {
  CGetL $arg
  String "\n"
  Concat
  Print
  PopC

  CGetL $arg
  RetC
}

.function N byref(inout N $foo) {
  Null
  Int 42
  SetL $foo
  RetM 2
}

.function ["__EntryPoint"("""y:0:{}""")] N main() {
  .declvars $funk;
  NullUninit
  NullUninit
  String "a"
  String "b"
  FCallFuncD <> 2 1 "" "" - "" "caller"
  PopC

  String "strval"
  SetL $funk
  PopC
  NullUninit
  NullUninit
  NullUninit
  CGetL $funk
  Null
  PopL $funk
  FCallFuncD <> 1 2 "1" "" - "" "byref"
  PopC
  Print
  PopC
  String "\n"
  Print
  PopC

  True
  RetC
}
